sp {mac*propose*initialize-mac
  (state <s> ^superstate nil
    -^name)
-->
  (<s> ^operator <o> +)
  (<o> ^name initialize-mac)}


sp {mac*apply*initialize-mac
  (state <s> ^operator.name initialize-mac)
-->
  (<s> ^name mac
  ^left-bank <l>
  ^right-bank <r>
  ^desired <d>)
  (<r> ^missionaries 0
  ^cannibals 0
  ^boat 0
  ^other-bank <l>)
  (<l> ^missionaries 3
  ^cannibals 3
  ^boat 1
  ^other-bank <r>)
  (<d> ^right-bank.missionaries 3
       ^right-bank.cannibals 3
       ^right-bank.boat 1)}




sp {mac*propose*operator*move-mac-boat1
  (state <s> ^name mac
    ^<< right-bank left-bank >> <bank>)
  (<bank> ^boat 1)
-->
(<s> ^operator <o> + =)
(<o> ^name move-mac-boat
  ^bank <bank>
  ^boat 1
  ^types 1)}

sp {mac*propose*operator*move-mac-boat2
  (state <s> ^name mac
    ^ << right-bank left-bank >> <bank>)
  (<bank> ^{ << cannibals missionaries >> <type> } > 1
    ^boat 1)
-->
(<s> ^operator <o> + =)
(<o> ^name move-mac-boat
^bank <bank>
^<type> 2
^boat 1
^types 1)}

sp {mac*propose*operator*move-mac-boat11
  (state <s> ^name mac
    ^ << right-bank left-bank >> <bank>)
  (<bank> ^missionaries > 0
    ^cannibals > 0
    ^boat 1)
-->
  (<s> ^operator <o> + =)
  (<o> ^name move-mac-boat
    ^bank <bank>
    ^missionaries 1
    ^cannibals 1
    ^boat 1
    ^types 2)}

#Begin rewrite: 
sp {apply*move-mac-boat*cannibals
  (state <s> ^operator <o>)
  (<o> ^name move-mac-boat
    ^cannibals <number>
    ^bank <bank>)
  (<bank> ^cannibals <bank-num>
    ^other-bank <obank>)
  (<obank> ^cannibals <obank-num>)
-->
  (<bank> ^cannibals <bank-num> -
           ^cannibals (- <bank-num> <number>))
  (<obank> ^cannibals <obank-num> -
           ^cannibals (+ <obank-num> <number>))}

sp {apply*move-mac-boat*missionaries
  (state <s> ^operator <o>)
  (<o> ^name move-mac-boat
    ^missionaries <number>
    ^bank <bank>)
  (<bank> ^missionaries <bank-num>
    ^other-bank <obank>)
  (<obank> ^missionaries <obank-num>)
-->
  (<bank> ^missionaries <bank-num> -
           ^missionaries (- <bank-num> <number>))
  (<obank> ^missionaries <obank-num> -
           ^missionaries (+ <obank-num> <number>))}

sp {apply*move-mac-boat*boat
  (state <s> ^operator <o>)
  (<o> ^name move-mac-boat
    ^boat <number>
    ^bank <bank>)
  (<bank> ^boat <bank-num>
    ^other-bank <obank>)
  (<obank> ^boat <obank-num>)
-->
  (<bank> ^boat <bank-num> -
          ^boat (- <bank-num> <number>))
  (<obank> ^boat <obank-num> -
          ^boat (+ <obank-num> <number>))}

sp {monitor*move-mac-boat
  (state <s> ^operator <o>)
  (<o> ^name move-mac-boat
    ^{ << cannibals missionaries >> <type> } <number>)
-->
  (write | Move | <number> | | <type>)}

#begin rewrite: mointor state left to use dot notation so translation is not confused by variable names cannibals #and missionaries being shared by two attributes right bank and left bank

sp {monitor*state*left
  (state <s> ^name mac
    ^left-bank.cannibals <cl>
    ^left-bank.missionaries <ml>
     ^left-bank.boat 1
    ^right-bank.cannibals <cr>
    ^right-bank.missionaries <mr>
     ^right-bank.boat 0)
  
-->
  (write (crlf) | M: | <ml> |, C: | <cl> | B ~~~ |
    | M: | <mr> |, C: | <cr> | |)}

  
sp {monitor*state*right
  (state <s> ^name mac
    ^left-bank.cannibals <cl>
    ^left-bank.missionaries <ml>
     ^left-bank.boat 0
    ^right-bank.cannibals <cr>
    ^right-bank.missionaries <mr>
     ^right-bank.boat 1)

-->
  (write (crlf) | M: | <ml> |, C: | <cl> | ~~~ B |
    | M: | <mr> |, C: | <cr> | |)}
#End rewrite

sp {mac*evaluate*state*failure*more*cannibals
  (state <s> ^desired <d>
    ^<< right-bank left-bank >> <bank>)
  (<bank> ^missionaries { <n> > 0 }
    ^cannibals > <n>)
-->
  (<s> ^failure <d>)}

#Begin rewrite: mac*apply*move-mac-boat*record*last-operator*types*1 re written for missionaries and cannibals

sp {mac*apply*move-mac-boat*record*last-operator*types*1*cannibals
  (state <s> ^name mac
    ^operator <o>)
  (<o> ^name move-mac-boat
    ^bank <bank>
    ^cannibals <n>
    ^types 1)
-->
  (<s> ^last-operator <o1>)
  (<o1> ^types 1
    ^bank <bank>
    ^type cannibals
    ^number <n>)}

sp {mac*apply*move-mac-boat*record*last-operator*types*1*missionaries
  (state <s> ^name mac
    ^operator <o>)
  (<o> ^name move-mac-boat
    ^bank <bank>
    ^cannibals <n>
    ^types 1)
-->
  (<s> ^last-operator <o1>)
  (<o1> ^types 1
    ^bank <bank>
    ^type missionaries
    ^number <n>)}


#End rewrite

sp {mac*apply*move-mac-boat*record*last-operator*types*2
  (state <s> ^name mac
    ^operator <o>)
  (<o> ^name move-mac-boat
    ^bank <bank>
    ^types 2)
-->
  (<s> ^last-operator <o1>)
  (<o1> ^types 2
    ^bank <bank>)}

#Begin rewrite for mac*apply*move-mac-boat*remove*old*last-operator rightbank and leftbank

sp {mac*apply*move-mac-boat*remove*old*last-operator*right
  (state <s> ^name mac
    ^operator <o>
    ^right-bank.other-bank <o-bank>
    ^last-operator <lo>)
  (<lo> ^bank <o-bank>)
-->
  (<s> ^last-operator <lo> -)}

sp {mac*apply*move-mac-boat*remove*old*last-operator*left
  (state <s> ^name mac
    ^operator <o>
    ^left-bank.other-bank <o-bank>
    ^last-operator <lo>)
  (<lo> ^bank <o-bank>)
-->
  (<s> ^last-operator <lo> -)}

#End rewrite

#Begin rewrite for mac*select*operator*prefer*inverse*failure*types*2 for cannibals and missionaries

sp {mac*select*operator*prefer*inverse*failure*types*2*cannibals
  (state <s> ^name mac
    ^operator <o> +
    ^failure <d>
    ^last-operator <lo>)
  (<o> ^name move-mac-boat
    ^cannibals <number>
    ^types 1)
  (<lo> ^types 1
    ^type cannibals
    ^number <number>)
-->
  (<s> ^operator <o> >)}

sp {mac*select*operator*prefer*inverse*failure*types*2*missionaries
  (state <s> ^name mac
    ^operator <o> +
    ^failure <d>
    ^last-operator <lo>)
  (<o> ^name move-mac-boat
    ^missionaries <number>
    ^types 1)
  (<lo> ^types 1
    ^type misisonaries
    ^number <number>)
-->
  (<s> ^operator <o> >)}

#End rewrite

sp {mac*select*operator*prefer*inverse*failure*types*1
  (state <s> ^name mac
    ^operator <o> +
    ^failure true
    ^last-operator.types 2)
  (<o> ^types 2)
-->
  (<s> ^operator <o> >)}

#Begin rewrite to say failure is false, instead of abscence of true, and iterate over missionaries and cannibals

sp {mac*select*operator*avoid*inverse*not*failure*1*cannibals
  (state <s> ^name mac
    ^operator <o> +
    -^failure true
    ^last-operator <lo>)
  (<o> ^types 1
    ^cannibals <number>)
  (<lo> ^types 1
    ^type cannibals
    ^number <number>)
-->
  (<s> ^operator <o> < )}

sp {mac*select*operator*avoid*inverse*not*failure*1*missionaries
  (state <s> ^name mac
    ^operator <o> +
    -^failure true
    ^last-operator <lo>)
  (<o> ^types 1
    ^missionaries <number>)
  (<lo> ^types 1
    ^type missionaries
    ^number <number>)
-->
  (<s> ^operator <o> < )}
#end rewrite

sp {mac*select*operator*avoid*inverse*not*failure*2
  (state <s> ^name mac
    ^operator <o> +
    -^failure true
    ^last-operator <lo>)
  (<o> ^types 2)
  (<lo> ^types 2)
-->
  (<s> ^operator <o> < )}
#Begin rewrite: for right bank and left bank comparisons, eliminate placeholders in attribute 

sp {mac*detect*state*success
  (state <s> ^right-bank.cannibals 3
    ^right-bank.missionaries 3)

-->
  (write (crlf) |The problem has been solved.|)
  (halt)}

#End rewrite





